10 REM Default movement rate
20 LET r=0
30 PROC initSprites(r)
40 PROC initScreen()
50 LET %s=0
60 ;
70 REPEAT
80 IF %s THEN SPRITE MOVE INT : ELSE SPRITE MOVE
90 IF INKEY$ =" " THEN LET %s=%1-s: BEEP .5,10
110 REPEAT UNTIL 0
120 ;
210 DEFPROC initSprites(rate)
220 SPRITE CLEAR
230 LOAD "mummy-r.spr" BANK 12
240 SPRITE BANK 12
250 REM Use batching mode
260 SPRITE STOP
270 REM Default movement rate
280 ;
290 REM Wanderer
300 SPRITE 0,150,10,1,1
310 SPRITE CONTINUE 0,0 TO 303 STEP 1 RUN ,0 TO 240 STEP 1 RUN ,1 TO 4, BIN 1000,rate+8
440 ;
450 REM Flame squad
460 REM Anchor sprite
470 SPRITE 7,254,204,37, BIN 1001, BIN 001
480 REM Relative sprites
490 SPRITE -8,16,0,2,1, BIN 110
500 SPRITE -9,0,16,4,1, BIN 110
510 SPRITE -10,16,16,6,1, BIN 110
520 REM Set movement and animation
530 SPRITE CONTINUE 7,50 TO 254 STEP -1 RUN ,50 TO 204 STEP -1 STOP ,36 TO 37, BIN 11001,rate+6
540 ;
550 REM Descender
560 SPRITE 11,100,0,51,1
570 SPRITE CONTINUE 11,0 TO 303 STEP 1 RUN ,200 STEP 1 RUN ,12 TO 15, BIN 1000,rate+7
580 ;
590 REM Beating heart
600 SPRITE 12,152,120,51,1
610 SPRITE CONTINUE 12,152 STOP ,120 STOP ,33 TO 34, BIN 1000000,15
620 ;
630 REM Climber
640 SPRITE 13,180,239,51,1
650 SPRITE CONTINUE 13,180 STOP ,0 TO 239 STEP -1 RUN ,7 TO 10, BIN 100,rate+6
660 ;
670 REM Zoomer
680 SPRITE 14,40,190,35,1
690 SPRITE CONTINUE 14,0 TO 302 STEP 2 RUN ,0 TO 238 STEP 2 RUN ,35, BIN 1000
700 ENDPROC
710 ;
720 DEFPROC initScreen()
730 LAYER CLEAR
740 PAPER 7: INK 0: BORDER 4: CLS
750 SPRITE PRINT 1: SPRITE BORDER 1
760 POKE 23658,0: REM turn off CAPS LOCK
770 LET %o=0: LET %p=0: LET %t=0
780 PRINT INVERSE 1;"SPACE"; INVERSE 0;" toggles framerate sync"
790 PRINT '"Try running at different speeds"'"(hold NMI and press 8)"
800 ENDPROC
810 ;
820 STOP
830 SAVE "spriteAutomove.bas"
