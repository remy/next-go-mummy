10 LOAD "mummy.spr" BANK 16,0,1280
20 SPRITE BANK 16
30 SPRITE CLEAR : SPRITE PRINT 1
100 LET x=152: LET y=104: LET z=0: LET %d= BIN 1: LET s=4
500 BORDER 6: PAPER 7: INK 0: CLS
505 REM draw bg grid
509 LAYER 1,1: CLS
510 FOR i=16 TO 176 STEP 40
511 LAYER 1,1: PRINT POINT 0,i;"████████████████████████████████"
512 LAYER 1,1: PRINT POINT 0,i+8;"████████████████████████████████"
513 NEXT i
514 FOR i=0 TO 256 STEP 32
515 LAYER 1,1: PRINT POINT i,0;"█████"
516 NEXT i
519 GO TO 1000
1000 REM main loop
1010 REM display sprites
1015 LAYER 1,1: PRINT POINT 0,184; INVERSE 1;"x:";x;" y:";y;" d:";%d;" z:";z
1020 SPRITE 0,x,y,z,%d
5000 REM movement
5010 IF INKEY$ ="q" THEN GO SUB 7010
5020 IF INKEY$ ="a" THEN GO SUB 7110
5030 IF INKEY$ ="o" THEN GO SUB 7210
5040 IF INKEY$ ="p" THEN GO SUB 7310
6000 GO TO 1010
7000 REM move up
7010 LET y=y-s
7020 LET %d = %d ^ BIN 00001000
7030 LET z=2
7040 RETURN
7100 REM move down
7110 LET y=y+s
7120 LET %d = %d ^ BIN 00001000
7130 LET z=3
7140 RETURN
7200 REM move left
7210 LET x=x-s
7220 LET %d= BIN 1001
7230 LET z=z+1: IF z > 1 THEN LET z=0
7240 RETURN
7300 REM move right
7310 LET x=x+s
7320 LET %d= BIN 1
7330 LET z=z+1: IF z > 1 THEN LET z=0
7340 RETURN

