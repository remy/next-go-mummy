10 ; Oh Mummy (clone), Remy Sharp 2020
20 RUN AT 2: REM 3.5Mhz
25 RANDOMIZE 5
30 ;
50 PROC init()
60 PROC initBaddies()
70 ;
80 ; define player and game state
90 LET %x=48: LET %y=48: LET %s=1:; s = sprite
100 LET %d=1: LET %z=4:; direction & speed
110 LET %t=3:; baddie speed
160 ;
200 ; game loop
201 ; PROC fps()
202 IF INKEY$ ="s" THEN GO TO 10:; restart
210 ;
220 ; move player
225 ; PRINT AT 0,0;"x:";%x;",y:";%y;";fps:";%f;";t:";%t;"     "
230 SPRITE 0,%x,%y,%s,%d
231 ;
235 LET %j= IN 31
240 IF %( IN $fbfe >> 0&1^1) THEN LET %j=%(j+@1000): REM Q
250 IF %( IN $fdfe >> 0&1^1) THEN LET %j=%(j+@0100): REM A
260 IF %( IN $dffe >> 1&1^1) THEN LET %j=%(j+@0010): REM O
270 IF %( IN $dffe >> 0&1^1) THEN LET %j=%(j+@0001): REM P
272 ; if I can't move on x plane, check y
275 IF %x MOD 48 <> 0 THEN GO TO %300
280 IF %j&@1000=@1000 THEN GO SUB 7000: REM up
290 IF %j&@0100=@0100 THEN GO SUB 7100: REM down
299 ; if I can't move on y plane, skip
300 IF %y MOD 48 <> 0 THEN GO TO 400
310 IF %j&@0010=@0010 THEN GO SUB 7200: REM left
320 IF %j&@0001=@0001 THEN GO SUB 7300: REM right
399 ;
400 ; check for map edge
401 ;
410 IF %x<48 THEN : LET %x=48: ELSE IF %x>240 THEN : LET %x=240
420 IF %y<48 THEN : LET %y=48: ELSE IF %y>192 THEN : LET %y=192
499 ;
500 ; baddies - current %i
501 ;
510 LET %b=0
549 ;
550 ; start baddie checks
580 LET %j=%6:; 7 props
589 ;
590 ; print baddie
600 SPRITE %b+1,%B(b*j),%B((b*j)+1),%B((b*j)+2),1
610 LET %i=%B((b*j)+3):
620 ; PRINT AT %b+1,0;"x:";%B(b*j);";y:";%B((b*j)+1);"    "
628 ;
629 ; if they can't change direction, just move them forward
630 IF %B((b*j)+i) MOD 48 <> 0 THEN GO TO %700
648 ;
649 ; change direction randomly
650 ; LET %B((b*j)+3)=% RND 2: LET %B((b*j)+4)=% RND 2
651 LET %B((b*j)+ 3 + RND 2)=% RND 2
658 ;
659 ; reassign %i after changing direction
660 LET %i=%B((b*j)+3)
661 ;
670 ; move baddie
671 ;
700 IF %B((b*j)+4)=0 THEN : LET %B((b*j)+i)=%B((b*j)+i)+t: GO TO 705:; move X
701 LET %B((b*j)+i)=%B((b*j)+i)-t: LET %B((b*j)+2)=7: ; move Y
708 ; check if baddie is on edge of map
709 ;
710 IF %B((b*j)+i)<48 THEN : LET %B((b*j)+i)=48: GO TO 650: ; rotate
720 IF %i=0 THEN GO TO 750
730 IF %B((b*j)+i)>192 THEN : LET %B((b*j)+i)=192: GO TO 650: ; rotate
740 GO TO 800
750 IF %B((b*j)+i)>240 THEN : LET %B((b*j)+i)=240: GO TO 650: ; rotate
797 ;
798 ; next baddie
799 ;
800 LET %b=%b+1: IF %b<3 THEN GO TO %550
998 ;
999 GO TO %200:; loop
6999 ;
7000 ; move up
7001 ;
7020 LET %y=%y-z
7030 LET %d=%d^ BIN 00001000
7040 LET %s=2
7050 RETURN
7099 ;
7100 ; move down
7101 ;
7120 LET %y=%y+z
7130 LET %d=%d^ BIN 00001000
7140 LET %s=3
7150 RETURN
7199 ;
7200 ; move left
7201 ;
7220 LET %x=%x-z
7230 LET %d= BIN 00001001
7240 LET %s=%s+1: IF %s>1 THEN LET %s=0
7250 RETURN
7299 ;
7300 ; move right
7301 ;
7320 LET %x=%x+z
7330 LET %d= BIN 00000001
7340 LET %s=%s+1: IF %s>1 THEN LET %s=0
7350 RETURN
7999 ;
8000 ;
8010 ; init
8020 ;
8030 DEFPROC init()
8035 LAYER 2,1: CLS
8040 LOAD "mummy-2.spr" BANK 13: REM load spritesheet
8050 LOAD "mummy-3.map" BANK 14: REM load tile map
8060 TILE BANK 13: REM point tiles to spritesheet
8070 TILE DIM 14,0,16,16: REM bank 14, offset 0, tilemap width 16 tiles, tile size 16
8080 TILE 16,12: REM print tile for 16 tile cols by 12 tile rows
8090 LOAD "mummy-2.spr" BANK 16,0,4096
8100 SPRITE BANK 13
8110 ENDPROC
8500 ;
8510 ; initBaddies: %B=Array([x,y,spr,angle(0: x, 1: y),direction(0: backward, 1: forward),alive])
8515 ; note: i*j = j properties per baddie
8520 ;
8530 DEFPROC initBaddies()
8535 LET %j=6 :; n props
8540 FOR %i=0 TO 2:
8550 LET %B( i*j)=% RND 4 * 48 + 48:; x
8560 LET %B((i*j)+1)=% RND 4 * 48 + 48:; y
8570 LET %B((i*j)+2)=5:; sprite
8580 LET %B((i*j)+3)=% RND 4:; direction bit
8590 LET %B((i*j)+4)=1:; alive
8591 LET %B((i*j)+5)=1:; sprite flags
8600 NEXT %i
8610 PAPER 255: INK 0: BORDER 0:; background black, text white
8620 SPRITE CLEAR : SPRITE PRINT 1
8650 ENDPROC
9799 ;
9800 DEFPROC fps()
9805 LOCAL %b
9810 LET %b=% PEEK 23672
9820 LET %f=%b-a
9830 IF %b<a THEN LET %f=%f+256
9840 LET %a=%b
9850 ENDPROC
9899 ;
9900 ;Quick save
9901 ;
9910 ; SPRITE PRINT 0: SAVE "mummy-3.bas" LINE 9999: STOP
9911 SPRITE PRINT 0: SAVE "mummy-3.bas": STOP
9996 ;
9997 ;Boot game
9998 ;
9999 PROC init(): GO TO 5
